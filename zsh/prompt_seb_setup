# vim:ft=zsh ts=2 sw=2 sts=2

# Characters
SEGMENT_SEPARATOR="\ue0b0"
PLUSMINUS="\u00b1"
BRANCH="\ue0a0"
DETACHED="\u27a6"
CROSS="\u2718"
LIGHTNING="\u26a1"
GEAR="\u2699"

prompt_git() {
  local color ref
  is_dirty() {
    test -n "$(git status --porcelain --ignore-submodules)"
  }
  ref="$vcs_info_msg_0_"
  if [[ -n "$ref" ]]; then
    if is_dirty; then
      color="%F{3}"
      ref="${ref} $PLUSMINUS"
    else
      color="%F{2}"
      ref="${ref}"
    fi
    if [[ "${ref/.../}" == "$ref" ]]; then
      ref="$BRANCH $ref"
    else
      ref="$DETACHED ${ref/.../}"
    fi
    # prompt_segment $color $PRIMARY_FG
    print -Pn $color" $ref"
  fi
}


# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
prompt_seb_precmd() {
  vcs_info
  # Define prompts.
  PROMPT="$(prompt_seb_main) "
  RPROMPT=''
}
prompt_seb_path() {
  print -n "%F{4}%20<…<%~%<<%F{7}% "
}
prompt_seb_end() {
  print -n " %F{4}❯%F{7}"
}
prompt_seb_main() {
  prompt_seb_path
  prompt_git
  prompt_seb_end
}

prompt_seb_setup() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_seb_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes false
  zstyle ':vcs_info:git*' formats '%b'
  zstyle ':vcs_info:git*' actionformats '%b (%a)'


}

prompt_seb_setup "$@"
